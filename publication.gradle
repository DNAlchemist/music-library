apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

project.ext.isSignatoryConfigured = {
    if (project.hasProperty('signing.keyId') && project.hasProperty('signing.password') && project.hasProperty('signing.secretKeyRingFile'))
        return true
    else
        logger.warn("No signatory is available to sign")
    return false
}.memoize()

def hasCredentials = hasProperty('ossrhUsername') && hasProperty('ossrhPassword');

uploadArchives.doFirst {
    if (!hasCredentials) {
        throw new InvalidUserDataException('Missing properties "ossrhUsername" and "ossrhPassword"')
    }
}

if (hasCredentials) {
    configureDeployment()
}

@SuppressWarnings(["GroovyAssignabilityCheck", "GrUnresolvedAccess"])
def configureDeployment() {

    signing {
        if (isSignatoryConfigured())
            sign configurations.archives
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment {
                    if (isSignatoryConfigured()) {
                        signing.signPom(it)
                    }
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Music library'
                    description 'Library for music search and download'
                    packaging 'jar'
                    url 'https://chest.one'

                    scm {
                        url 'https://github.com/DNAlchemist/music-library'
                        connection 'scm:git:git://github.com/DNAlchemist/music-library.git'
                        developerConnection 'scm:git:git://github.com/DNAlchemist/music-library.git'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            id 'dnalchemist'
                            name 'Mikhalev Ruslan'
                            email 'mikhalev.ruslan@gmail.com'
                        }
                    }
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }
        }
    }

}